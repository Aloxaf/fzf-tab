#!/hint/zsh
emulate -L zsh -o extended_glob

zmodload zsh/mapfile

# receive arguments
local pid=$1 header_lines=$2 active_group_style=$3 tmp_dir=$4 offset=$5

# read completion list
local -a list=(${(f)mapfile[$tmp_dir/completions.$pid]})

# get total group count
if (( $#list > 10000 )); then
  local -Ua total=(${(f)"$(print -l ${list:$header_lines} | grep -a -oP '^\x1b\[[0-9;]*m')"})
else
  local -Ua total=(${(M)${list:$header_lines}#$'\x1b['[0-9;]#*m})
fi

# get current group index, start from 1
local current=1
if [[ -f $tmp_dir/current-group.$pid ]]; then
  current=$(( $(<$tmp_dir/current-group.$pid) + offset ))
fi
(( current > $#total )) && current=1
(( current == 0 )) && current=$#total
echo $current > $tmp_dir/current-group.$pid

# configure style of active header
local sgr_on='' sgr_off=''
case $active_group_style in
  (bold)          sgr_on=$'\x1b[1m';    sgr_off=$'\x1b[22m' ;;
  (underline)     sgr_on=$'\x1b[4m';    sgr_off=$'\x1b[24m' ;;
  (bold,underline) sgr_on=$'\x1b[1;4m'; sgr_off=$'\x1b[22;24m' ;;
  (none)          sgr_on='';            sgr_off='' ;;
  (*)             sgr_on=$'\x1b[1m';    sgr_off=$'\x1b[22m' ;;  # fallback
esac

# print headers
if (( header_lines != 0 )); then
  local -i i
  local line
  for i in {1..$header_lines}; do
    line=$list[i]
    if [[ $line == *${total[current]}* ]]; then
      # Apply style only to the group label (up to the first ']'), then disable it
      line=${line/${total[current]}(#b)([^]]##)\]/${total[current]}$sgr_on${match[1]}]$sgr_off}
    fi
    print -r -- "$line"
  done
fi

# print current group
if (( $#list > 10000 )); then
  print -l ${list:$header_lines} | grep -a -F "${total[current]}"
else
  print -l ${(M)${list:$header_lines}:#${total[current]}*}
fi
