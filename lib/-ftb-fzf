#!/hint/zsh

local ftb_preview_init="
local -a _ftb_compcap=(\"\${(@f)\"\$(</tmp/fzf-tab/compcap.$$)\"}\")
local -a _ftb_groups=(\"\${(@f)\"\$(</tmp/fzf-tab/groups.$$)\"}\")
local bs=\$'\2'
# get descriptoin
export desc=\${\${\"\$(<{f})\"%\$'\0'*}#*\$'\0'}
# get ctxt for current completion
local -A ctxt=(\"\${(@0)\${_ftb_compcap[(r)\${(b)desc}\$bs*]#*\$bs}}\")
# get group
if (( \$+ctxt[group] )); then
  export group=\$_ftb_groups[\$ctxt[group]]
fi
# get real path if it is file
if (( \$+ctxt[isfile] )); then
  export realpath=\${(Qe)~\${:-\${ctxt[IPREFIX]}\${ctxt[hpre]}}}\${(Q)desc}
fi
# get original word
export word=\$ctxt[word]
"
local binds=tab:down,btab:up,change:top,ctrl-space:toggle
local fzf_command fzf_flags fzf_preview debug_command tmp

-ftb-zstyle -s fzf-command fzf_command || fzf_command=fzf
-ftb-zstyle -a fzf-bindings tmp && binds+=,${(j:,:)tmp}
-ftb-zstyle -a fzf-flags fzf_flags
-ftb-zstyle -s fzf-preview fzf_preview

-ftb-zstyle -a debug-command debug_command && {
  typeset -g choices=("$(${(eX)debug_command} $fzf_flags)")
  choices=("${(@f)choices}")
  return
}

[[ -d /tmp/fzf-tab ]] || mkdir /tmp/fzf-tab
print -rl -- $_ftb_compcap > /tmp/fzf-tab/compcap.$$
print -rl -- $_ftb_groups  > /tmp/fzf-tab/groups.$$

local -i lines=$#_ftb_compcap header_lines=$#_ftb_headers

typeset -g choices
choices="$($fzf_command \
  --ansi \
  --bind=$binds \
  --color=hl:$(( $#_ftb_headers == 0 ? 188 : 255 )) \
  --cycle \
  --delimiter='\x00' \
  --expect=$continuous_trigger,$print_query \
  --header-lines=$#_ftb_headers \
  --height=${FZF_TMUX_HEIGHT:=$(( lines > LINES / 2 ? LINES / 2 : lines ))} \
  --layout=reverse \
  --multi \
  --nth=2,3 \
  --print-query \
  --query=$_ftb_query \
  --tiebreak=begin \
  ${fzf_preview:+--preview=$ftb_preview_init$fzf_preview} \
  $fzf_flags
)"
choices=("${(@f)choices}")

command rm /tmp/fzf-tab/{compcap,groups}.$$ 2>/dev/null
