#!/hint/zsh

# import math functions
autoload -Uz zmathfunc
zmathfunc

local tmp_dir=${TMPPREFIX:-/tmp/zsh}-fzf-tab-$USER
[[ -d $tmp_dir ]] || command mkdir $tmp_dir

local ftb_preview_init
ftb_preview_init=$(<"$FZF_TAB_HOME/lib/-ftb-preview.tpl")

# Inject resolved paths (these MUST refer to the correct pid used to create the files)
local compcap_file="$tmp_dir/compcap.$$"
local groups_file="$tmp_dir/groups.$$"

ftb_preview_init=${ftb_preview_init//__FTB_COMPCAP__/${(q)compcap_file}}
ftb_preview_init=${ftb_preview_init//__FTB_GROUPS__/${(q)groups_file}}

# Inject the dump of 'words'
ftb_preview_init=${ftb_preview_init//__FTB_WORDS_DUMP__/$(typeset -p words)}

# command substitution strips all trailing newlines from the output
ftb_preview_init+=$'\n'

local default_binds=tab:down,btab:up,change:top,ctrl-space:toggle,bspace:backward-delete-char/eof,ctrl-h:backward-delete-char/eof
local fzf_command fzf_flags fzf_preview debug_command tmp switch_group active_group_style fzf_pad fzf_min_height binds 
local ret=0

-ftb-zstyle -s fzf-command fzf_command || fzf_command=fzf
-ftb-zstyle -a fzf-bindings-default tmp && binds=${(j:,:)tmp} || binds=$default_binds
-ftb-zstyle -a fzf-bindings tmp && binds+=,${(j:,:)tmp}
-ftb-zstyle -a fzf-flags fzf_flags
-ftb-zstyle -s fzf-preview fzf_preview
-ftb-zstyle -a switch-group switch_group || switch_group=(F1 F2)
-ftb-zstyle -s fzf-pad fzf_pad || fzf_pad=2
-ftb-zstyle -s fzf-min-height fzf_min_height || fzf_min_height=0
-ftb-zstyle -b use-fzf-default-opts use_fzf_default_opts || use_fzf_default_opts="no"
-ftb-zstyle -s active-group-style active_group_style || active_group_style=bold

-ftb-zstyle -a debug-command debug_command && {
  ${(eX)debug_command} $fzf_flags
  return
}

# Anonymous helper block to avoid introducing a new global function.
#
# Normalize `active-group-style` into a canonical, deterministic string.
#
# Users may specify one or more style tokens separated by ',' or '+', with
# arbitrary whitespace and any casing, e.g.:
#   "bold"
#   "underline"
#   "underline + bold"
#   "BOLD,Underline"
#
# This block:
#   - lowercases input
#   - removes whitespace
#   - treats '+' as an alias for ','
#   - splits into tokens
#   - keeps only supported tokens: bold, underline
#   - de-duplicates and sorts tokens alphabetically
#   - joins them into a single comma-separated canonical string
#
# Output:
#   - sets $REPLY to one of:
#       "bold"
#       "underline"
#       "bold,underline"
#       "none"            (if no valid tokens were provided)
#
() {
  emulate -L zsh -o extended_glob

  local raw=${1:-bold}
  raw=${(L)raw}
  raw=${raw//[[:space:]]/}
  raw=${raw//+/,}

  local -a toks out bad
  local t

  toks=(${(s:,:)raw})

  for t in $toks; do
    [[ -z $t ]] && continue
    case $t in
      (bold|underline) out+=($t) ;;
      (*) bad+=($t) ;;
    esac
  done

  # Here we can decide the policy for unknown tokens:
  # - ignore them (current behavior)
  # - or next commented line: strict fallback to bold:
  # (( $#bad )) && out=(bold)

  # Canonicalize: sort + join
  if (( $#out == 0 )); then
    REPLY=none
    return 0
  fi
  out=(${(ou)out})            # (o)=alphabetical sort; (u)=unique
  REPLY="${(j:,:)out}"        # "bold,underline"
} "$active_group_style"
local normalized_active_group_style=$REPLY   # e.g. "bold" or "bold,underline" or "none"

print -rl -- $_ftb_compcap > $tmp_dir/compcap.$$
print -rl -- $_ftb_groups  > $tmp_dir/groups.$$
print -r -- ${ftb_preview_init/{f}/\$1} > $tmp_dir/ftb_preview_init.$$

binds=${binds//{_FTB_INIT_}/. $tmp_dir/ftb_preview_init.$$ {f} $'\n'}

local -i header_lines=$#_ftb_headers
local -i lines=$(( $#_ftb_compcap + fzf_pad + header_lines ))
local reload_command="$commands[zsh] -f $FZF_TAB_HOME/lib/ftb-switch-group $$ $header_lines $normalized_active_group_style $tmp_dir"

# detect if we will use tmux popup
local use_tmux_popup=0
if [[ $fzf_command == "ftb-tmux-popup" ]]; then
  use_tmux_popup=1
fi

if (( ! use_tmux_popup )); then
  # fzf will cause the current line to refresh, so move the cursor down.
  echoti cud1 >/dev/tty
  # reset cursor before call fzf
  echoti cnorm >/dev/tty 2>/dev/null
fi

command cat > $tmp_dir/completions.$$

local dd='gdd'
if (( ${+commands[$dd]} == 0 )) ; then
  dd='dd'
fi
if (( ${+commands[$dd]} == 0 )) ; then
  dd='true' # nop if dd is not installed
fi

_ftb_query="${_ftb_query}$(command "$dd" bs=1G count=1 status=none iflag=nonblock < /dev/tty 2>/dev/null)" || true

local fzf_default_opts=''
if [[ "$use_fzf_default_opts" == "yes" ]]; then
  fzf_default_opts=$FZF_DEFAULT_OPTS
fi

local -a initial_command=($commands[zsh] -f $FZF_TAB_HOME/lib/ftb-switch-group $$ $header_lines $normalized_active_group_style $tmp_dir 0)

FZF_DEFAULT_OPTS=$fzf_default_opts SHELL=$ZSH_NAME $fzf_command \
  --ansi \
  --bind=$binds \
  --bind="${switch_group[1]}:reload($reload_command -1),${switch_group[2]}:reload($reload_command 1)" \
  --cycle \
  --delimiter='\x00' \
  --expect=$continuous_trigger,$print_query,$accept_line \
  --header-lines=$header_lines \
  --height=${FZF_TMUX_HEIGHT:=$(( min(max(lines, fzf_min_height), LINES / 3 * 2)  ))} \
  --layout=reverse \
  --multi \
  --nth=2,3 \
  --print-query \
  --query=$_ftb_query \
  --tiebreak=begin \
  ${fzf_preview:+--preview=${ftb_preview_init}$fzf_preview} \
  $fzf_flags < <("${initial_command[@]}") || ret=$?

if (( ! use_tmux_popup )); then
  echoti civis >/dev/tty 2>/dev/null
  echoti cuu1 >/dev/tty
fi

command rm $tmp_dir/*.$$ 2>/dev/null
return $ret
