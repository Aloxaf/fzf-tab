#!/hint/zsh

local binds=tab:down,btab:up,change:top,ctrl-space:toggle
local fzf_command fzf_flags tmp

-ftb-zstyle -s fzf-command fzf_command || fzf_command=fzf
-ftb-zstyle -a fzf-bindings tmp && binds+=:${(j|:|)tmp}
-ftb-zstyle -a fzf-flags fzf_flags

[[ -d /tmp/fzf-tab ]] || mkdir /tmp/fzf-tab
print -rl -- $_ftb_compcap > /tmp/fzf-tab/compcap.$$
print -rl -- $_ftb_groups  > /tmp/fzf-tab/groups.$$

local -i lines=$#_ftb_compcap

typeset -g choices
choices="$($fzf_command \
  --ansi \
  --bind=$binds \
  --color=hl:$(( $#headers == 0 ? 188 : 255 )) \
  --cycle \
  --delimiter='\x00' \
  --expect=$continuous_trigger,$print_query \
  --header-lines=$#headers \
  --height=${FZF_TMUX_HEIGHT:=$(( lines > LINES / 2 ? LINES / 2 : lines ))} \
  --layout=reverse \
  --multi \
  --nth=2,3 \
  --print-query \
  --query=$query \
  --tiebreak=begin \
  $fzf_flags
)"
choices=("${(@f)choices}")

command rm /tmp/fzf-tab/{compcap,groups}.$$ 2>/dev/null
